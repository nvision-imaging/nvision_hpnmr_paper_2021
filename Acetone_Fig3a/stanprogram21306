;noediff.2
;avance-version (12/01/11)
;NOE-difference
;pseudo 2D
;using frequency list to define irradiation frequencies
;doing dummy scans for all blocks
;
;$CLASS=HighRes
;$DIM=2D
;$TYPE=
;$SUBTYPE=
;$COMMENT=


#include <Avance.incl>
#include <Delay.incl>


define list<frequency> noelist=<$FQ2LIST>


"d11=30m"


"DELTA1=d1-d20"


1 ze
  d11 pl14:f2
  3m noelist:f2
  3m noelist.inc
2 d11
  DELTA1
  d20 cw:f2
  4u do:f2
  p1 ph1
  go=2 ph31
  d11 wr #0  if #0
  lo to 1 times td1
  3m rf #0
  3m noelist.res
  lo to 1 times l4
exit


ph1=0 2 2 0 1 3 3 1 
ph31=0 2 2 0 1 3 3 1


;pl1 : f1 channel - power level for pulse (default)
;pl14: f2 channel - power level for cw decoupling
;p1 : f1 channel -  90 degree high power pulse
;d1 : relaxation delay; 1-5 * T1
;d11: delay for disk I/O                             [30 msec]
;d20: irradiation time
;l4: l4 = number of averages = (total number of scans) / NS
;ns: 8 * n
;ds: 4
;td1: number of experiments

;define FQ2LIST (irradiation frequencies)
;               (list has to be stored in "/u/exp/stan/nmr/lists/f1")

;this pulse program produces a ser-file (PARMOD = 2D)



;$Id: noediff.2,v 1.7 2012/01/31 17:49:27 ber Exp $
